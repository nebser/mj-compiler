package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
	private boolean errorDetected = false;
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
    
    public boolean isErrorDetected() {
    	return errorDetected;
    }
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

terminal PROGRAM, CONST, COMMA, CLASS, EXTENDS, ABSTRACT, ASSIGN;
terminal String IDENT, CHARACTER, BOOL, MINUS, PLUS;
terminal Integer NUMBER;
terminal SEMI_COLON, LBRACKET, RBRACKET, LBRACE, RBRACE;
terminal LPARENTHESIS, RPARENTHESIS, VOID;
terminal IF, ELSE, FOR, BREAK, CONTINUE, RETURN, READ, PRINT;
terminal INCREMENT, DECREMENT, OR, AND;
terminal MULTIPLY, DIVIDE, MOD;
terminal NEW, COLON;
terminal EQUALS, DIFFERS, GREATER, GREATER_OR_EQUAL, LESS, LESS_OR_EQUAL;

nonterminal DeclSection, DeclList, DeclElem, MethodDeclSection, MethodDecl;
nonterminal VarDecl, AbstractClassDecl, ClassDecl;
nonterminal VarDeclSection, VarDeclList;
nonterminal StatementList, Statement, AnyMethodDeclSection, AnyMethodDeclList, AnyMethodDecl;
nonterminal AbstractMethodDecl, MethodDeclList, ExtendsStmt;
nonterminal Condition, Matched, Unmatched, OptionalDesignatorStatement, ForStatement;
nonterminal Assignop, ActPars, ExprList, Relop;
nonterminal CondTerm, CondFact;
nonterminal DesignatorSentence;
nonterminal FieldDeclSection, FieldDeclList, FieldDecl;
nonterminal GlobalMethodDeclList, GlobalMethodDecl;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgramName, Program, ConstDefinition, MethodHeader, GlobalMethodHeader;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorStatement;
nonterminal rs.ac.bg.etf.pp1.util.ObjList ConstList, FormParsList, FormPars, GlobalVarDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, ClassName, ReturnType, ReturnExpr;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Factor, Expr, FactorList, AddExpr, Term;
nonterminal rs.ac.bg.etf.pp1.util.Var Var;
nonterminal rs.ac.bg.etf.pp1.util.VarList VarList;
nonterminal Addop, Mulop;

Program ::= (Program) PROGRAM ProgramName:p DeclSection LBRACE GlobalMethodDeclList RBRACE;

ProgramName ::= (ProgramName) IDENT:progName;

DeclSection ::= (DeclarationSection) DeclList
				|
				(NoDeclarationSection) /* epsilon */
				;

DeclList ::= (Declarations) DeclList DeclElem
			|
			(Declaration) DeclElem
			;
			
DeclElem ::= (ConstantDeclarations) CONST Type ConstList
			|
			(GlobalVariableDeclarations) GlobalVarDecl
			|
			(GlobalAbstactClassDeclarations) AbstractClassDecl
			|
			(GlobalClassDeclarations) ClassDecl
			;

ConstList ::= (Constants) ConstDefinition COMMA ConstList
			|
			 (Constant) ConstDefinition SEMI_COLON
			;
			
ConstDefinition ::= (NumberConstant) IDENT:name ASSIGN NUMBER:value
					|
					(CharacterConstant) IDENT:name ASSIGN CHARACTER:value
					|
					(BoolConstant) IDENT:name ASSIGN BOOL:value
					;
					
GlobalVarDecl ::= (GlobalVarDecl) Type VarList SEMI_COLON;

VarDecl ::= (RegularVarDecl) Type VarList SEMI_COLON
			|
			(ErrorDecl) error SEMI_COLON:l
			{: parser.report_error("Iregularna definicija promenljive u liniji " + lleft, null); :}
			;

VarList ::= (Variables) Var COMMA VarList
			|
			(Variable) Var
			;
			
Var ::= (PrimitiveVar) IDENT:ident
		|
		(ArrayVar) IDENT:ident LBRACKET RBRACKET
		;

MethodDeclSection ::= (MethodDeclarationSection) LBRACE MethodDeclList RBRACE
					|
					(NoMethodDeclarationSection) /* epsilon */
					;
					
MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
					|
					(MethodDeclaration) MethodDecl
					;
					
GlobalMethodDeclList ::= (GlobalMethodDeclarations) GlobalMethodDeclList GlobalMethodDecl
						 |
						 (GlobalMethodDeclaration) GlobalMethodDecl
						 ;
						 
GlobalMethodDecl ::= (GlobalMethodDecl) GlobalMethodHeader VarDeclSection LBRACE StatementList RBRACE;

GlobalMethodHeader ::= (GlobalMethodHeader) ReturnType IDENT:ident LPARENTHESIS FormPars RPARENTHESIS;
					
ClassDecl ::= (ClassDecl) CLASS ClassName ExtendsStmt LBRACE FieldDeclSection MethodDeclSection RBRACE;

ClassName ::= (ClassName) IDENT:ident;

ExtendsStmt ::= (ExtendsStatement) EXTENDS IDENT
					| 
					(NoExtendsStatement) /* epsilon */
					;
					
FieldDeclSection ::= (FieldDeclarationSection) FieldDeclList
					|
					(NoFieldDeclarationSection) /* epsilon */
					;
					
FieldDeclList ::= (FieldDeclarations) FieldDeclList FieldDecl
				  |
				  (FieldDeclaration) FieldDecl
				  ;
				  
FieldDecl ::= (FieldDecl) Type VarList SEMI_COLON;

VarDeclSection ::= (VariableDeclarationSection) VarDeclList
					|
					(NoVariableDeclarationSection) /* epsilon */
					;
					
VarDeclList ::= (VariablesDeclarations) VarDeclList VarDecl
				|
				(VariablesDeclarationsElem) VarDecl
				;
				
AbstractClassDecl ::= (AbstractClassDecl) ABSTRACT CLASS ClassName ExtendsStmt LBRACE FieldDeclSection AnyMethodDeclSection RBRACE;

AnyMethodDeclSection ::= (AnyMethodDeclarationSection) LBRACE AnyMethodDeclList RBRACE
						|
						(NoAnyMethodDeclarationSection) /* epsilon */
						;
						
AnyMethodDeclList ::= (AnyMethodDeclarations) AnyMethodDeclList AnyMethodDecl
						|
						(AnyMethodDeclaration) AnyMethodDecl
						;
						
AnyMethodDecl ::= (RealMethodDeclaration) MethodDecl
				|
				(AbstractMethodDeclaration) ABSTRACT MethodHeader SEMI_COLON
				;
					
MethodDecl ::= (MethodDecl) MethodHeader VarDeclSection LBRACE StatementList RBRACE;

MethodHeader ::= (MethodHeader) ReturnType IDENT:ident LPARENTHESIS FormPars RPARENTHESIS;

ReturnType ::= (NonVoidType) Type
				|
				(VoidType) VOID
				;
				
FormPars ::= (FormalParameters) FormParsList
			|
			(NoFormalParameters) /* epsilon */
			;
			
FormParsList ::= (ParameterList) FormParsList COMMA Type Var
				 |
				 (SinglePar) Type Var
				 ;
			
StatementList ::= (Statements) StatementList Statement
					|
					(SingleStatement) Statement
					;
					
Statement ::= (MathedStmt) Matched
				|
			  (UnmatchedStmt) Unmatched
			  |
			  (ForStmt) FOR LPARENTHESIS OptionalDesignatorStatement SEMI_COLON Condition SEMI_COLON OptionalDesignatorStatement RPARENTHESIS Statement
			  ;
			  
Matched ::= (DesignatorStmt) DesignatorSentence
			|
			(MatchedIf) IF LPARENTHESIS Condition RPARENTHESIS Matched ELSE Matched
			|
			(StmtList) LBRACE StatementList RBRACE
			|
			(Break) BREAK SEMI_COLON
			|
			(Continue) CONTINUE SEMI_COLON
			|
			(Return) RETURN ReturnExpr SEMI_COLON
			|
			(Read) READ LPARENTHESIS Designator RPARENTHESIS SEMI_COLON
			|
			(Print) PRINT LPARENTHESIS Expr RPARENTHESIS SEMI_COLON
			|
			(PrintWithNumber) PRINT LPARENTHESIS Expr COMMA NUMBER:width RPARENTHESIS SEMI_COLON
			;
			
DesignatorSentence ::= (DesignatorSen) DesignatorStatement SEMI_COLON
						|
					   (ErrorDesignatorSen) error SEMI_COLON:l
					   {: parser.report_error("Iregularna sintaksa za iskaz dodele u liniji " + lleft, null); :}
					   ;
			
ReturnExpr ::= (RetExpr) Expr
				|
				(NoReturnExpr) /* epsilon */
				;
			
Unmatched ::= (UnmatchedIf) IF LPARENTHESIS Condition RPARENTHESIS Statement
				|
			  (UnmatchedElse) IF LPARENTHESIS Condition RPARENTHESIS Matched ELSE Unmatched
			  ;
			  
OptionalDesignatorStatement ::= (OptionalDesignatorStmt) DesignatorStatement
								|
								(NoDesignatorStmt) /* epsilon */
								;
								
DesignatorStatement ::= (AssignDesignator) Designator Assignop Expr
						|
						(FunctionCall) Designator LPARENTHESIS ActPars RPARENTHESIS
						|
						(IncrementDesignator) Designator INCREMENT
						|
						(DecrementDesignator) Designator DECREMENT
						;
						
ActPars ::= (ActualPars) ExprList
			|
			(NoActualPars) /* epsilon */
			;
			
ExprList ::= (ExpressionList) ExprList COMMA Expr
			|
			 (Expression) Expr
			 ;
			 
Condition ::= (CondTermList) Condition OR CondTerm
				|
				(SingleCondTerm) CondTerm
				;
				
CondTerm ::= (CondFactList) CondTerm AND CondFact
			|
			(SingleCondFact) CondFact
			; 
			
CondFact ::= (ExprOp) Expr Relop Expr
			|
			(SingleExpr) Expr
			;
			
Expr ::= (UnaryMinusExpression) MINUS AddExpr
		 |
		 (NoMinusExpression) AddExpr
		 ;
		 
AddExpr ::= (AddExpression) AddExpr Addop Term
			|
			(TermExpression) Term
			;
				
Addop ::= (Minus) MINUS
			|
		  (Plus) PLUS
		  |
		  (PlusEquals) PLUS ASSIGN
		  |
		  (MinusEquals) MINUS ASSIGN
		  ;
		  
Term ::= (MulTerm) Term Mulop Factor
		 |
		 (FactorTerm) Factor
		 ;
		  
Mulop ::= (Multiply) MULTIPLY
			|
		  (Divide) DIVIDE
		  	|
		  (Mod) MOD
		  |
		  (MultiplyEquals) MULTIPLY ASSIGN
		  |
		  (DivideEquals) DIVIDE ASSIGN
		  |
		  (ModEquals) MOD EQUALS
		  ;
		  
Factor ::= (DesignatorFactor) Designator
			|
			(DesignatorWithActParsFactor) Designator LPARENTHESIS ActPars RPARENTHESIS
			|
			(NumberFactor) NUMBER:value
			|
			(CharacterFactor) CHARACTER:value
			|
			(BoolFactor) BOOL:value
			|
			(NewArrayFactor) NEW Type LBRACKET Expr RBRACKET
			|
			(NewObjectFactor) NEW Type
			|
			(ExpressionFactor) LPARENTHESIS Expr RPARENTHESIS
			;
					
Designator ::= (SimpleDesignator) IDENT:ident
				|
			   (ArrayDesignator) Designator LBRACKET Expr RBRACKET
			   |
			   (ObjectDesignator) Designator COLON IDENT:ident
			   ;
					

Relop ::= (Equals) EQUALS
		  |
		  (Differs) DIFFERS
		  |
		  (Greater) GREATER
		  |
		  (GreaterOrEqual) GREATER_OR_EQUAL
		  |
		  (Less) LESS
		  |
		  (LessOrEqual) LESS_OR_EQUAL
		  ;				
						
Assignop ::= ASSIGN;
				
Type ::= (Type) IDENT:ident;